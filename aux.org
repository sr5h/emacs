#+STARTUP: content

* transparency
** codes
#+BEGIN_SRC emacs-lisp
(defun transparency (value)
  "Sets the transparency of the frame window. 0=transparent/100=opaque"
  (interactive "nTransparency Value 0 - 100 opaque:")
  (set-frame-parameter (selected-frame) 'alpha value))
#+END_SRC

* fix slime autodoc temporarily

** codes
#+BEGIN_SRC emacs-lisp
;; fix for slime <v2.21 in emacs 26 (eldoc-message now only takes 1 arg)
(when (version< slime-version "2.25")
  (defun slime-autodoc--async%-fixed (context multilinep doc)
    (cl-destructuring-bind (doc cache-p) doc
      (unless (eq doc :not-available)
        (when cache-p
          (slime-autodoc--cache-put context doc))
        ;; Now that we've got our information,
        ;; get it to the user ASAP.
        (when (eldoc-display-message-p)
          (eldoc-message (slime-autodoc--format doc multilinep))))))

  (defun slime-autodoc-manually-fixed ()
    "Like autodoc informtion forcing multiline display."
    (interactive)
    (let ((doc (slime-autodoc t)))
      (cond (doc (eldoc-message doc))
            (t (eldoc-message nil)))))

  (defun slime-autodoc-space-fixed (n)
    "Like `slime-space' but nicer."
    (interactive "p")
    (self-insert-command n)
    (let ((doc (slime-autodoc)))
      (when doc
        (eldoc-message doc))))

  (advice-add 'slime-autodoc--async% :override #'slime-autodoc--async%-fixed)
  (advice-add 'slime-autodoc-manually :override #'slime-autodoc-manually-fixed)
  (advice-add 'slime-autodoc-space :override #'slime-autodoc-space-fixed))
#+END_SRC
